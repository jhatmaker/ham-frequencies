# 
# NOTES: 
#  Icom software needs to be imported in 100 records per import
#

###########################################################
# $A$1
# CH No
###########################################################
='idaho standard'!A2

###########################################################
# $B$1
# Frequency
###########################################################
='idaho standard'!B2

###########################################################
# $C$1
# CH ID
###########################################################


###########################################################
# $D$1
# Dup
###########################################################

=if('idaho standard'!$B2 <> "",
   if('idaho standard'!$H2 = "+","DUP+",if('idaho standard'!$H2 = "-","DUP-","")),"")

###########################################################
# $E$1
# Offset
###########################################################
=if('idaho standard'!$I2 <> "", 'idaho standard'!$I2,"")

###########################################################
# $F$1
# TS
###########################################################
=if('idaho standard'!B2>150,"25k","5k")


###########################################################
# $G$1
# Mode
###########################################################
='idaho standard'!J2

###########################################################
# $H$1
# Name
###########################################################
=if('idaho standard'!$B2 <> "",
  if('idaho standard'!$C2 <> "",
	IF(LEN('idaho standard'!$C2) > 6,
	UPPER(CONCAT(LEFT(REGEXREPLACE('idaho standard'!$C2," ", ""),3),
	           RIGHT(REGEXREPLACE('idaho standard'!$C2," ", ""),3)))
	,UPPER('idaho standard'!$C2)
	)
        ,""
  )
 ,""
)

###########################################################
# $i$1
# SKIP
###########################################################

###########################################################
# $J$1
# TONE
###########################################################

=if('idaho standard'!$B2 =""
   , ""
   ,if('idaho standard'!$E2 = "" 
       ,""
       , vlookup('idaho standard'!$E2,{"TONE","TONE";"TSQL","TSQL";"DTCS","DCS";"",""},2,0)))

###########################################################
# $K$1
# Repeater Tone
###########################################################
=CONCAT(
if('idaho standard'!$F2 <> "",'idaho standard'!$F2,"88.5")
,"Hz")

###########################################################
# $L$1
# TSQL Frequency
###########################################################
=CONCAT(
if('idaho standard'!$G2 <> "",'idaho standard'!$G2,"88.5")
,"Hz")


###########################################################
# $M$1
# DTCS Code
###########################################################
'023

###########################################################
# $N$1
# DTCS Polarity 
###########################################################
'BOTH-N
